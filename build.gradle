plugins {
    id 'groovy'
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'com.gradleup.shadow' version '9.2.2'
}

ext {
    inceptionYear = '2015'
    url = 'https://github.com/matthewprenger/CurseGradle'
}

version = '1.5.0'
group = 'com.matthewprenger'
description = 'Gradle plugin to upload artifacts to CurseForge'
base {
    archivesName = 'CurseGradle'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()

    implementation group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    implementation group: 'com.google.guava', name: 'guava', version: '19.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.2'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn javadocJar, sourcesJar

[compileJava, compileGroovy].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}

tasks.named('shadowJar') {
    archiveClassifier.set('')
    mergeServiceFiles()

    relocate 'com.google', 'com.matthewprenger.cursegradle.google'
    relocate 'gnu.trove', 'com.matthewprenger.cursegradle.gnu.trove'
    relocate 'org.apache', 'com.matthewprenger.cursegradle.org.apache'
}

tasks.named('assemble') {
    dependsOn tasks.named('shadowJar')
}

tasks.named('jar') {
    enabled = false
}

gradlePlugin {
    website.set(project.url)
    vcsUrl.set('https://github.com/matthewprenger/CurseGradle')

    plugins {
        register('curseGradlePlugin') {
            id = 'com.matthewprenger.cursegradle'
            displayName = 'CurseGradle'
            description = project.description
            implementationClass = 'com.matthewprenger.cursegradle.CurseGradlePlugin'
            tags.set(['curseforge', 'publish', 'minecraft'])
        }
    }
}